buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath 'de.thetaphi:forbiddenapis:3.9'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    // https://github.com/SgtSilvio/gradle-maven-central-publishing
    id 'io.github.sgtsilvio.gradle.maven-central-publishing' version '0.4.0'
    id 'io.codearte.nexus-staging' version '0.30.0'

    // https://gradle-pitest-plugin.solidsoft.info/
    id 'info.solidsoft.pitest' version '1.15.0'
}

apply plugin: 'de.thetaphi.forbiddenapis'
apply plugin: 'jacoco'
apply plugin: 'signing'

java {
  toolchain {
	  sourceCompatibility = JavaVersion.VERSION_17
  }
}

group = 'org.dstadler'
base {
    archivesName = 'commons-test'
}

repositories {
    mavenCentral()
}

forbiddenApis {
    suppressAnnotations = ['org.dstadler.commons.util.SuppressForbidden']
    bundledSignatures = [ 'jdk-reflection', 'commons-io-unsafe-2.18.0', 'jdk-internal' ]
}
forbiddenApisMain {
    // 'jdk-unsafe', 'jdk-system-out'
    bundledSignatures += [ 'jdk-deprecated', 'jdk-internal', 'jdk-non-portable' ]
}

configurations {
    all*.exclude group: 'javax.mail', module:'mail' // not needed here
    all*.exclude group: 'javax.activation', module:'activation' // not needed here
}

dependencies {
    implementation 'org.apache.commons:commons-lang3:3.18.0'
    implementation 'commons-io:commons-io:2.20.0'
    implementation 'org.dstadler:commons-dost:1.4.3'

    // dumbster is somewhat broken, but we use it in SafeCloseSmtpServer...
    // dumbster includes javax.mail-api 1.3.2, but this has no jar?!
    // also javax.mail-api 1.6.0-rc1 and newer causes MockSMTPServerTest.testSendEmail() to time out!
    // probably because the old version of dumbster and our SafeCloseSmtpServer do not implement something...
    api 'javax.mail:javax.mail-api:1.5.6'
    api 'dumbster:dumbster:1.6'

    // for TestHelpers we need junit during normal compile...
    implementation 'org.junit.jupiter:junit-jupiter-engine:5.13.4'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.13.4'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.13.4'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.13.4'

    testImplementation 'org.apache.commons:commons-email:1.5'
    testImplementation 'com.sun.activation:javax.activation:1.2.0'     // needed for JDK 11
}

wrapper {
    gradleVersion = '8.14.2'
}

test {
    systemProperties = System.properties as Map<String, ?>

    // enable to show standard out and standard error of the test JVM(s) on the console
    // testLogging.showStandardStreams = true

    // http://forums.gradle.org/gradle/topics/jacoco_related_failure_in_multiproject_build
    systemProperties['user.dir'] = workingDir

    useJUnitPlatform()
}

jacoco {
    toolVersion = '0.8.13'
}

jacocoTestReport {
    reports {
        xml.required = true
    }
}

tasks.register('sourcesJar', Jar) {
    archiveClassifier = 'sources'
    from sourceSets.main.allJava
}

tasks.register('javadocJar', Jar) {
    archiveClassifier = 'javadoc'
    from javadoc
}

tasks.withType(Jar).configureEach { task ->
    task.doLast {
        ant.checksum algorithm: 'md5', file: it.archivePath
        ant.checksum algorithm: 'sha1', file: it.archivePath
    }
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'commons-test'
            from components.java

            pom {
                name = 'Common Test Utilities'
                description = 'Common testing utilities I find useful in many of my projects.'
                url = 'https://github.com/centic9/commons-test'
                licenses {
                    license {
                        name = 'BSD 2-Clause "Simplified" License'
                        url = 'https://github.com/centic9/commons-test/blob/master/LICENSE.md'
                    }
                }
                developers {
                    developer {
                        id = 'centic9'
                        name = 'Dominik Stadler'
                    }
                }
                scm {
                    connection = 'scm:git@github.com:centic9/commons-test'
                    developerConnection = 'scm:git@github.com:centic9/commons-test'
                    url = 'https://github.com/centic9/commons-test'
                }
            }
        }
    }
    repositories {
        maven {
            // change to point to your repo, e.g. http://my.org/repo
            //url = layout.buildDirectory.dir('repo')
            url = 'https://oss.sonatype.org/service/local/staging/deploy/maven2/'
            credentials(PasswordCredentials)
            //snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots/')
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}
